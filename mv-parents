#!/usr/bin/perl -w

# Fri 15 Feb 22:51:30 GMT 2019
(my $email='ch%christianjaeger,ch')=~ tr/%,/@./;

use strict; use warnings FATAL => 'uninitialized';

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "usage: $myname [options] src... target

  Runs

     cp -a --parents [options] -- src... target
     rm -f -- src...

  Options:
    --         terminate option processing
    --help     help
    --dry-run  don't act
    --to ...   target, instead of taking the last argument
    -...       other options passed to cp (only options without argument
               accepted for now)

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}


my @options;
our $opt_dry;
our $opt_to;
our $verbose;
my @paths;

for (my $i=0; $i< @ARGV; $i++) {
    $_= $ARGV[$i];
    if ($_ eq "--") {
        $i++;
        # copy rest
        push @paths, @ARGV[$i..$#ARGV];
        last;
    }
    if (/^--?(.+)/s) {
        if (! length $1) {
            usage "stdin and stdout pointless";
        }
        my $a= $1;
        if ($a=~ /^dry(-run)?$/) {
            $opt_dry= 1
        } elsif ($a=~ /^v(erbose)?$/) {
            $verbose= 1
        } elsif ($a=~ /^h(elp)?$/) {
            usage;
        } elsif ($a=~ /^to$/) {
            $i++;
            $opt_to= $ARGV[$i]
              // usage "missing argument after --to";
        } else {
            push @options, $_;
        }
    } else {
        push @paths, $_
    }
}

my (@src, $target);
if (defined $opt_to) {
    $target= $opt_to;
    @src= @paths;
} else {
    unless (@paths) {
        usage "missing arguments";
    }
    @src= @paths;
    $target= pop @src;
}

# empty src is allowed (it's a nop); have to stop otherwise error from
# `cp`
exit unless @src;

use Chj::xperlfunc ":all";
use Chj::singlequote "singlequote_sh";

my $doprefix= $opt_dry ? "would run " : "+ ";
sub Do {
    if ($opt_dry or $verbose) {
        print $doprefix, join(" ", map {singlequote_sh $_} @_), "\n";
    }
    if (! $opt_dry) {
        xxsystem @_;
    }
}

Do "cp", "-a", "--parents", @options, "--", @src, $target;
Do "rm", "-f", "--", @src;

