#!/usr/bin/perl -w

my $copyright= <<'COPYRIGHT';
# Copyright 2008-2021 by Christian Jaeger <ch@christianjaeger.ch>
# Published under the same terms as perl itself
COPYRIGHT

use strict;
use warnings;
use warnings FATAL => 'uninitialized';
use experimental 'signatures';

use Getopt::Long;
use Chj::IO::Command;
use Chj::singlequote;

my ($email_full)= $copyright=~ / by ([^\n]*)/s;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname packagename

  Uses 'apt-cache rdepends' and 'dpkglist' to only list the rdepends
  which are locally installed (unless --all is given).

  Options:

    --all  show rdepends even if not locally installed

  ($email_full)
";
exit (@_ ? 1 : 0);
}

our $verbose=0;
our $opt_all;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
           "all"=> \$opt_all,
	   ) or exit 1;
usage unless @ARGV==1;

our ($packagename)=@ARGV;

our $packagename_installed= $opt_all ? undef : do {
    my $h={};
    my $in= Chj::IO::Command->new_sender ("dpkglist");
    while (<$in>) {
	chomp;
	$$h{$_}=1;
    }
    $in->xxfinish;
    $h
};

sub is_installed ($packagename) {
    $opt_all ? 1 : $$packagename_installed { $packagename }
}


package PFLANZE::Package {
    use FP::Struct ['is_or',
                    'packagename'];
    _END_
}
PFLANZE::Package::constructors->import;

sub packagename_rdepends {
    my ($packagename)=@_;
    my $in= Chj::IO::Command->new_sender ("apt-cache","rdepends","--",$packagename);
    my $cnt=$in->xcontent;
    $in->xxfinish;
    my $die= sub {
	die join ("",@_,": ",singlequote($cnt));
    };
    $cnt=~ s/^$packagename\s+//s
      or &$die("was expecting packagename in output first");
    $cnt=~ s/^Reverse Depends:\s+//s
      or &$die("was expecting 'Reverse Depends:' in output");
    my @dep= split /\s+/, $cnt;
    [
     map {
	 my $is_or= s/^\|//;
	 /^[\w-]+\z/
	   or die "hm invalid packagename or not?: '$_'";
	 Package($is_or, $_)
     } @dep
    ]
}

sub packagename_rdepends_local {
    my ($packagename)=@_;
    my $rdepends= packagename_rdepends ($packagename);
    [
     grep {
         is_installed($_->packagename)
     } @$rdepends
    ]
}


#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;
