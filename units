#!/usr/bin/perl -w

# Sat Aug 27 20:56:49 EDT 2011
(my $email='XXX%YYY,ch')=~ tr/%,/@./;

use strict; use warnings FATAL => 'uninitialized';
use utf8;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname value[s]

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

usage unless @ARGV;
my $first=$ARGV[0];
usage if $first eq "-h" or $first eq "--help";

utf8::decode($myname);

sub feet2m($) {
    my ($v)=@_;
    ($v * 0.3048)
}
sub m2feet($) { # lol
    my ($v)=@_;
    ($v / 0.3048)
}

our $conv=
  +{
    "째C"=> sub {
	my ($v)=@_;
	(($v*9/5)+32)." 째F"
    },
    "째F"=> sub {
	my ($v)=@_;
	(($v - 32)*5/9)." 째C"
    },
    "feet"=> sub {
	my ($v)=@_;
	feet2m($v)." m, ".($v * 12). " in"
    },
    "miles"=> sub {
	my ($miles)=@_;
	my $feet= $miles * 5280;
	feet2m($feet)." m, $feet feet"
    },
    "km"=> sub {
        my ($km)=@_;
        my $feet= m2feet($km*1000);
        my $miles= $feet / 5280;
        "$miles miles"
    },
    do {
	my $f= sub {
	    my ($gal)=@_;
	    my $imperial_gallon= 4.54609; # l, used in the United
                                          # Kingdom, Canada, and some
                                          # Caribbean nations
	    my $US_gallon= 3.785; # l, used in the US and some Latin
                                  # American and Caribbean countries

	    # While there is no official symbol for the gallon (as
	    # there are for SI units), gal is in common use.
	    "$gal gal (imperial) = ".($gal * $imperial_gallon)." l"
	      .", $gal gal (US) = ".($gal * $US_gallon)." l"
	};
	(gal=> $f,
	 gallon=> $f)
    },
    knot=> sub {
	# unit of speed equal to one nautical mile (1.852 km) per
	# hour, approximately 1.15078 mph

	# a vessel travelling at 1 knot along a meridian travels
	# approximately one minute of geographic latitude in one hour.
	
	# Worldwide, the knot is used in meteorology, and in maritime
	# and air navigation
	
	my ($knot)=@_;
	"$knot knots = ".($knot * 1.852). " km/h"
    },
    pound=> sub {
	my ($pound)=@_;
	# https://en.wikipedia.org/wiki/Pound_(mass)#Avoirdupois_pound
	# well, is this the right one? for ounces, yes, but otherwise?
	# Really, krst.
	"$pound Avoirdupois pounds = about ".($pound * 0.454)." kg"
    },
    do {
	my $ounce= sub {
	    my ($ounce)=@_;
	    # The common avoirdupois ounce (approximately 28.3 g) is 1/16
	    # of a common avoirdupois pound; this is the United States
	    # customary and British imperial ounce.
	    "$ounce ounces = about ".($ounce * 0.454 / 12)." kg"
	};
	(ounce=> $ounce,
	 "oz."=> $ounce)
    },
   }->{$myname}
  or die "unknown name '$myname'";

print &$conv ($_)."\n"
    or die $!
    for @ARGV;


#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;
